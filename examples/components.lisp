(in-package #:org.shirakumo.alloy.examples)

(define-example components (screen)
  "The various component types that Alloy supports for user interaction"
  (let* ((window (windowing:make-window screen :min-size (alloy:px-size 700 480)))
         (focus (make-instance 'alloy:vertical-focus-list :focus-parent window))
         (layout (make-instance 'alloy:grid-bag-layout :layout-parent window :col-sizes '(200 T) :row-sizes '(30) :growth-policy :vertical)))
    (macrolet ((ex (place type &rest args)
                 `(progn (alloy:enter ,(string type) layout)
                         (alloy:represent ,place ',type ,@args :layout-parent layout :focus-parent focus))))
      (ex "Hello" alloy:button)
      (let ((value NIL))
        (ex value alloy:switch))
      (let ((value NIL))
        (ex value alloy:checkbox))
      (let ((value 0))
        (ex value alloy:radio :active-value 1))
      (let ((value 0))
        (ex value alloy:ranged-slider))
      (let ((value 0))
        (ex value alloy:wheel))
      (let ((value NIL))
        (ex value alloy:combo-set :value-set '((NIL . "False") (T . "True"))))
      (let ((value ""))
        (ex value alloy:input-line))
      (let ((value NIL))
        (ex value alloy:symb))
      (let ((value ""))
        (ex value alloy:input-box))
      (let ((value #(0 1 2 3 4 -4 -3 -2 -1 0)))
        (ex value alloy:plot)))))
